/*
 * Copyright 2018 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * or in the "license" file accompanying this file. This file is distributed
 * on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */

version = "0.6.0-SNAPSHOT"
group = "com.amazon.ion"

apply plugin: 'java-library'
apply plugin: "maven-publish"
apply plugin: "maven"
apply plugin: "signing"

jar.baseName = 'ion-hive-serde'

// only applicable for src/main/java source set
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Publishing
// -------------------------------------------------------------------------------

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

task singleJar(dependsOn: 'build', type: Jar) {
    baseName = "${jar.baseName}-all"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "ion-hive-serde"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Ion Hive SerDe"
                packaging = "jar"
                url = "https://github.com/amzn/ion-hive-serde"
                description = "A Apache Hive SerDe (short for serializer/deserializer) for the Ion file format."
                scm {
                    connection = "scm:git@github.com:amzn/ion-hive-serde.git"
                    developerConnection = "scm:git@github.com:amzn/ion-hive-serde.git"
                    url = "git@github.com:amzn/ion-hive-serde.git"
                }

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        name = "Amazon Ion Team"
                        email = "ion-team@amazon.com"
                        organization = "Amazon Labs"
                        organizationUrl = "https://github.com/amzn"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://aws.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    // Included in the single-jar
    compile "com.amazon.ion:ion-java:${ionjava_version}"
    compile "com.amazon.ion:ion-java-path-extraction:${pathextraction_version}"

    // Provided by the Hive installation
    compileOnly "org.apache.hive:hive-serde:${hive_version}"
    compileOnly "org.apache.hive:hive-exec:${hive_version}", { transitive = false }
    compileOnly "org.apache.hadoop:hadoop-common:${hadoop_version}"
    compileOnly "org.apache.hadoop:hadoop-main:${hadoop_version}"
    compileOnly "org.apache.hadoop:hadoop-mapreduce-client-core:${hadoop_version}"
}

