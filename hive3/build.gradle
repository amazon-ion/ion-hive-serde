/*
 * Copyright 2018 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * or in the "license" file accompanying this file. This file is distributed
 * on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */
plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

def hive_version = '[3.1.2,3.1.17)'
version = "1.3.0-SNAPSHOT"
group = "com.amazon.ion"

apply plugin: 'java-library'
apply plugin: "maven-publish"
apply plugin: "signing"

// only applicable for src/main/java source set
sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    // Dependencies that get bundled into the fat Jar
    bundled
    // Dependencies provided by Hive
    hiveRuntime
    // Dependencies used by the test code that are not shared with anything else
    testRunner

    implementation.extendsFrom(bundled, hiveRuntime)
    testImplementation.extendsFrom(testRunner)
}

shadowJar {
    configurations = [project.configurations.bundled]
}

task shadowJarTest(type: Test) {
    group "verification"
    dependsOn(shadowJar)
    classpath = shadowJar.outputs.files +
            project.configurations.hiveRuntime +
            project.configurations.testRunner +
            project.sourceSets.test.output
}

check {
    dependsOn(shadowJarTest)
}

// Publishing
// -------------------------------------------------------------------------------

tasks.withType(Jar) {
    archiveBaseName.set("ion-hive3-serde")
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "ion-hive3-serde"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Ion Hive SerDe"
                packaging = "jar"
                url = "https://github.com/amazon-ion/ion-hive-serde"
                description = "An Apache Hive SerDe (short for serializer/deserializer) for the Ion file format."
                scm {
                    connection = "scm:git@github.com:amazon-ion/ion-hive-serde.git"
                    developerConnection = "scm:git@github.com:amazon-ion/ion-hive-serde.git"
                    url = "git@github.com:amazon-ion/ion-hive-serde.git"
                }

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        name = "Amazon Ion Team"
                        email = "ion-team@amazon.com"
                        organization = "Amazon Labs"
                        organizationUrl = "https://github.com/amazon-ion"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://aws.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    // Included in the single-jar
    bundled("com.amazon.ion:ion-java:${ionjava_version}")
    bundled("com.amazon.ion:ion-java-path-extraction:${pathextraction_version}") {
        // ion-java-path-extraction uses Kotlin only for testing
        exclude(group: "org.jetbrains.kotlin")
    }

    // Provided by the Hive installation
    hiveRuntime "org.apache.hive:hive-serde:${hive_version}"
    hiveRuntime "org.apache.hive:hive-exec:${hive_version}", { transitive = false }
    hiveRuntime "org.apache.hadoop:hadoop-common:${hadoop_version}"
    hiveRuntime "org.apache.hadoop:hadoop-main:${hadoop_version}"
    hiveRuntime "org.apache.hadoop:hadoop-mapreduce-client-core:${hadoop_version}"

    // Test framework dependencies
    testRunner "pl.pragmatists:JUnitParams:[1.1.0,1.2.0)"
    testRunner("org.junit.jupiter:junit-jupiter:5.7.1")
    testRunner("org.junit.vintage:junit-vintage-engine")
    testRunner("org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version")
}

