/*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at:
 *
 *      http://aws.amazon.com/apache2.0/
 *
 * or in the "license" file accompanying this file. This file is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific
 * language governing permissions and limitations under the License.
 *
 */

task createTmpDir {
    doLast {
        mkdir 'docker-tmp/input'
        mkdir 'docker-tmp/output'
    }
}

task removeTmpDir {
    doLast {
        delete 'docker-tmp'
    }
}

task startDocker(type: Exec, dependsOn: 'copySerDeJar') {
    workingDir './docker'
    commandLine 'docker-compose', 'up', '-d'
}

task stopDocker(type: Exec) {
    workingDir './docker'
    commandLine 'docker-compose', 'down'
}

task copySerDeJar {
    dependsOn ':serde:singleJar'

    doLast {
        copy {
            from file('../serde/build/libs/serde-all.jar')
            into file('./docker-tmp/serde/')
        }
    }
}

task tailHiveLog(type: Exec, dependsOn: 'testSetUp') {
    commandLine 'docker', 'exec', 'hive-server', 'tail', '-f', '/tmp/root/hive.log'
}

task testSetUp {
    dependsOn createTmpDir
    dependsOn startDocker
    dependsOn copySerDeJar
}

task testTearDown {
    dependsOn stopDocker
    dependsOn removeTmpDir
}

/**
 * Tests will in order:
 * - created shared directories
 *
 * - start docker containers
 * - run all tests
 * - stop docker containers
 * - remove shared directories
 */
test {
    dependsOn testSetUp
    finalizedBy 'testTearDown'

    // only run tests through Suite classes to speed it up
    scanForTestClasses = false
    include "**/*Suite.class"
}

dependencies {
    testCompile project(":serde")

    testCompile "org.apache.hive:hive-jdbc:${hive_version}"

    // compile only dependencies are not transitive so we need to redeclare some here
    testCompile "org.apache.hive:hive-exec:${hive_version}", { transitive = false }
    testCompile "org.apache.hadoop:hadoop-common:${hadoop_version}"
}
