/*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at:
 *
 *      http://aws.amazon.com/apache2.0/
 *
 * or in the "license" file accompanying this file. This file is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific
 * language governing permissions and limitations under the License.
 */

// sets up kotlin plugin
buildscript {
    ext.kotlin_version = '1.2.61'
    ext.ionjava_version = '1.0.+'
    ext.hive_version = '1.1.+'
    ext.hadoop_version = '2.6.+'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: "kotlin"
    apply plugin: 'java-library'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    configurations {
        // to make compileOnly available to testing scope
        testCompile.extendsFrom(compileOnly)
    }

    dependencies {
        // using kotlin to make tests less verbose
        testCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

        testCompile "junit:junit:4.+"
        testCompile "pl.pragmatists:JUnitParams:1.1.+"
    }

    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion = "8.12"
        ignoreFailures = false
        maxWarnings = 0
        maxErrors = 0
        configDir = file("$rootProject.projectDir/config/checkstyle")
    }
    tasks.withType(Checkstyle) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    apply plugin: 'findbugs'
    tasks.withType(FindBugs) {
        excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")

        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    // disable findbugs for testing as it doesn't work well for Kotlin
//    check.dependsOn.remove('findbugsTest')
}
